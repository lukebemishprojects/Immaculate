pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://maven.lukebemish.dev/releases/' }
    }
}

plugins {
    id 'dev.lukebemish.conventions' version '0.1.10'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0' apply false
    id 'dev.lukebemish.managedversioning' version '1.2.26' apply false
}

gradle.lifecycle.beforeProject {
    group = 'dev.lukebemish' + (project.path == ':' ? '' : '.immaculate' + project.path.split(':')[0..-2].join('.'))

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'dev.lukebemish.managedversioning'

    managedVersioning {
        versionFile.set rootProject.isolated.projectDirectory.file('version.properties')
        versionPRs()
        versionSnapshots()
    }

    managedVersioning.apply()
    managedVersioning.publishing.mavenSnapshot(publishing)
    managedVersioning.publishing.mavenStaging(publishing)
    managedVersioning.publishing.mavenPullRequest(publishing)

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
    }

    tasks.register('publishCentral') {
        group = 'publishing'
    }

    tasks.withType(Sign).configureEach {
        // Gradle broke something in 8.8 here, and now you can't publish plugins to other mavens if signing is disabled...
        enabled System.getenv('GPG_KEY') !== null
    }
}

rootProject.name = 'immaculate'

include 'wrapper'
include 'wrapper:eclipse-jdt'
include 'wrapper:google-java-format'
include 'wrapper:palantir-java-format'
